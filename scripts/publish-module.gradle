apply plugin: 'maven-publish'
apply plugin: 'signing'

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                from components.release
                
                artifact androidSourcesJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Android EditText for currency values.'
                    url = 'https://github.com/gmcristiano/currency-edit-text'
                    licenses {
                        license {
                            name = 'EditTextCurrency License'
                            url = 'https://github.com/gmcristiano/currency-edit-text/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'gmcristiano'
                            name = 'Cristiano Correia'
                            email = 'cristiano_1990@hotmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/gmcristiano/currency-edit-text.git'
                        developerConnection = 'scm:git:ssh://github.com/gmcristiano/currency-edit-text.git'
                        url = 'https://github.com/gmcristiano/currency-edit-text/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}
